apply from: 'dependencies.gradle'

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = 'com.github.ralexey'
    version = '1.9.14'

    repositories {
        google()
        jcenter()
    }

    ext {
        isLibrary = false
    }
}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion versions.compile_sdk

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    versionCode 1914
                    versionName "1.9.14"
                }
            }
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def bintrayUser = properties.getProperty('bintray.user')
        def bintrayApikey = properties.getProperty('bintray.apikey')

        if(project.isLibrary) {
            configure(project) {
                apply plugin: 'com.github.dcendents.android-maven'
                group = project.group                               // Maven Group ID for the artifact

                install {
                    repositories.mavenInstaller {
                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/ralexey/barcodescanner'

                            scm {
                                url 'scm:git@github.com:ralexey/barcodescanner.git'
                                connection 'scm:git@github.com:ralexey/barcodescanner.git'
                                developerConnection 'scm:git@github.com:ralexey/barcodescanner.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'dm77'
                                    name 'Dushyanth Maguluru'
                                    email 'dushyanth.maguluru@gmail.com'
                                }
                            }
                        }
                    }
                }

                apply plugin: 'maven'

                version = '1.9.14'

                if (project.hasProperty("android")) { // Android libraries
                    task sourcesJar(type: Jar) {
                        classifier = 'sources'
                        from android.sourceSets.main.java.srcDirs
                    }
                } else { // Java libraries
                    task sourcesJar(type: Jar, dependsOn: classes) {
                        classifier = 'sources'
                        from sourceSets.main.allSource
                    }
                }


                artifacts {
                    archives sourcesJar
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1'
}
